plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.pixerylabs.android.essentia'
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32
        versionName "1.1"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        ndk {
            debugSymbolLevel = 'FULL'
            abiFilters = ["arm64-v8a", "armeabi-v7a", "x86_64"]
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-21', '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_shared'
                cFlags '-O3'
                cppFlags '-Wall', '-O3', "-std=c++17", '-latomic', "-fPIC", "-DANDROID", '-DGTEST_HAS_TR1_TUPLE=0', '-DHAVE_EIGEN3=1', '-DEIGEN_PERMANENTLY_DISABLE_STUPID_WARNINGS', '-D__STDC_CONSTANT_MACROS', '-DEIGEN_MPL2_ONLY'
            }
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.name}-${defaultConfig.versionName}.aar"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

dependencies {
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
}
